
// Добавить элемент Поле в таблицу формы "Список"
//
// Параметры:
//  оФорма	 - Управляемая форма - форма в которой в таблицу с именем "Список" будет добавлено
//    дополнительно поле таблицы
//  сПолеНаименование	 - Строка - наименование добавляемого поля
// 
// Возвращаемое значение:
//  Булево - признак успешного выполнения
//
Функция PEN006_ТаблицаФормыДобавитьЭлемент(Знач оФорма, Знач сПолеНаименование) Экспорт
	Перем бВыполненоБезОшибок;
	Перем оСписок, оЭлементПеред, элементСписка;
	
	бВыполненоБезОшибок = Ложь;
	
	Если ПустаяСтрока(сПолеНаименование) Тогда
		Возврат бВыполненоБезОшибок;
	КонецЕсли;
	
	оСписок = оФорма.Элементы.Найти("Список");
	оЭлементПеред = оФорма.Элементы.Найти("Дата");
	Если оСписок = Неопределено ИЛИ оЭлементПеред = Неопределено Тогда
		Возврат бВыполненоБезОшибок;
	КонецЕсли;
	
	Если НЕ ПолеСпискаДоступно(оФорма, сПолеНаименование) Тогда
		Возврат бВыполненоБезОшибок;
	КонецЕсли;
	
	элементСписка = оФорма.Элементы.Вставить(СтрШаблон("%1_%2", "PEN006", сПолеНаименование), Тип("ПолеФормы"), оСписок, оЭлементПеред);
	элементСписка.Вид = ВидПоляФормы.ПолеКартинки;
	элементСписка.Видимость = Истина;
	элементСписка.Заголовок = "Почта";
	элементСписка.Подсказка = "Документ был отправлен по e-mail или был распечатан конверт.";
	элементСписка.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	элементСписка.ПутьКДанным = СтрШаблон("%1.%2", "Список", сПолеНаименование);
	элементСписка.КартинкаЗначений = БиблиотекаКартинок[СтрШаблон("%1_%2", "PEN006", сПолеНаименование)];
	элементСписка.КартинкаШапки = БиблиотекаКартинок[СтрШаблон("%1_%2_h", "PEN006", сПолеНаименование)];
	
	бВыполненоБезОшибок = Истина;
	Возврат бВыполненоБезОшибок;
КонецФункции

Функция ПолеСпискаДоступно(Знач оФорма, Знач сПутьКПолю)
	Перем оДоступныеПоля, ПолеКомпоновки, бПолеДоступно;
	
	бПолеДоступно = Истина;
	ПолеКомпоновки = Новый ПолеКомпоновкиДанных(сПутьКПолю);
	оДоступныеПоля = оФорма.Список.КомпоновщикНастроек.Настройки.ДоступныеПоляВыбора;
	Если оДоступныеПоля.НайтиПоле(ПолеКомпоновки) = Неопределено Тогда
		бПолеДоступно = Ложь;
	КонецЕсли;
	
	Возврат бПолеДоступно;
КонецФункции
