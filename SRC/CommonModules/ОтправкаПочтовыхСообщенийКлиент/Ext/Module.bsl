
&ИзменениеИКонтроль("ОтправитьПоЭлектроннойПочте")
Процедура PEN006_ОтправитьПоЭлектроннойПочте(КомандыОтправки, АдресКомандОтправки, ОбъектыОтправки, ЕстьОшибки, ДополнительныеПараметры)

	ОписаниеКоманд = Новый Массив;
	ПечатныеФормы = Новый Массив;

	Для Каждого Команда Из КомандыОтправки Цикл

		ОписаниеКоманды = ОтправкаПочтовыхСообщенийВызовСервера.ОписаниеКомандыОтправки(Команда, АдресКомандОтправки);

		ОписаниеКоманды.Вставить("ОбъектыОтправки", ОбъектыОтправки);

		Если ОписаниеКоманды.МенеджерПечати = "СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки" Тогда

			Для Каждого КлючИЗначение Из ОписаниеКоманды.ДополнительныеПараметры Цикл
				ОписаниеКоманды.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
			КонецЦикла;

			ОписаниеКоманды.Вставить("ЭтоОтчет", Ложь);
			ОписаниеКоманды.Вставить("Вид", ПредопределенноеЗначение("Перечисление.ВидыДополнительныхОтчетовИОбработок.ПечатнаяФорма"));

			ПараметрыИсточника = Новый Структура;
			ПараметрыИсточника.Вставить("ИдентификаторКоманды", ОписаниеКоманды.Идентификатор);
			ПараметрыИсточника.Вставить("ОбъектыНазначения"   , ОбъектыОтправки);
			ОписаниеКоманды.Вставить("ПараметрыИсточника", ПараметрыИсточника);

		КонецЕсли;

		ОписаниеКоманды.ДополнительныеПараметры.Вставить("ФормироватьСтандартноеОписаниеПисьма", Ложь);
		ОписаниеКоманд.Добавить(ОписаниеКоманды);

	КонецЦикла;

	Если ОписаниеКоманд.Количество() > 0 Тогда
		ПараметрыПисьма = ОтправкаПочтовыхСообщенийВызовСервера.ПараметрыЭлектронногоПисьма(ОписаниеКоманд,
		ЕстьОшибки, ДополнительныеПараметры);
		Если ЗначениеЗаполнено(ПараметрыПисьма.Вложения) И НЕ ЕстьОшибки Тогда
			КлючеваяОперация = "БыстраяОтправкаДокументаПоЭлектроннойПочте";
			ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, КлючеваяОперация);
			#Удаление
			РаботаСПочтовымиСообщениямиКлиент.СоздатьНовоеПисьмо(ПараметрыПисьма);
			#КонецУдаления
			#Вставка
			стПараметры = Новый Структура();
			стПараметры.Вставить("ОбъектыОтправки", ОбъектыОтправки);
			ОписаниеОповещения = Новый ОписаниеОповещения("PEN006_ПослеОтправкиПисьма", PEN006_ОбщийМодульКлиент, стПараметры);
			РаботаСПочтовымиСообщениямиКлиент.СоздатьНовоеПисьмо(ПараметрыПисьма, ОписаниеОповещения);
			#КонецВставки
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры
